*>****************************************************************
*> Author: Joseph Warren
*> Date: 3/27/2019
*> Purpose: Read a sale from the index file
*> Tectonics: cobc
*>*****************************************************************
IDENTIFICATION DIVISION.
PROGRAM-ID. READ_SALE.
ENVIRONMENT DIVISION.
    INPUT-OUTPUT SECTION.
    FILE-CONTROL.
    SELECT OPTIONAL INDEX-FILE-SALE
        ASSIGN TO '../../INDEXES/SALE.IDX'
        ORGANIZATION IS INDEXED
        ACCESS IS DYNAMIC
        RECORD KEY IS IDX-saleID
        ALTERNATE RECORD KEY IS IDX-empID WITH DUPLICATES
        ALTERNATE RECORD KEY IS IDX-carVIN WITH DUPLICATES
        ALTERNATE RECORD KEY IS IDX-custID WITH DUPLICATES
        ALTERNATE RECORD KEY IS IDX-saleDate WITH DUPLICATES.

DATA DIVISION.
FILE SECTION.
FD INDEX-FILE-SALE
        RECORD CONTAINS 161 CHARACTERS.
    COPY SALE_DEF REPLACING ==:TAG:== BY ==IDX==.

WORKING-STORAGE SECTION.
01 MORE-RECORDS         PIC A(3)    VALUE 'YES'.

LINKAGE SECTION.
COPY SALE_DEF REPLACING ==:TAG:== BY ==LS==.
01 PARAMETRES.
   02 PA-RETURN-CODE PIC 99 VALUE 0.
   02 PA-OPTION-CODE PIC 99 VALUE 0.
   02 PA-LAST-SALEID PIC 9(5).
   02 PA-LAST-DATE.
      05 PA-YEAR   PIC 9(4).
      05 PA-MONTH  PIC 9(2).
      05 PA-DAY    PIC 9(2).

PROCEDURE DIVISION USING LS-Sale, PARAMETRES.
MAIN-PROCEDURE.
   EVALUATE PA-OPTION-CODE
   WHEN 00
      PERFORM 200-SINGLE-ACCESS
   WHEN 01
      PERFORM 201-MULTI-ACCESS
   END-EVALUATE
EXIT PROGRAM.

200-SINGLE-ACCESS.
   MOVE LS-saleID TO IDX-saleID
   OPEN INPUT INDEX-FILE-SALE
   READ INDEX-FILE-SALE INTO LS-Sale
      INVALID KEY MOVE 23 TO PA-RETURN-CODE
   END-READ
   CLOSE INDEX-FILE-SALE
EXIT PARAGRAPH.

201-MULTI-ACCESS.
   MOVE PA-LAST-DATE TO IDX-saleDate
   OPEN INPUT INDEX-FILE-SALE
   START INDEX-FILE-SALE KEY IS EQUAL IDX-saleDate
      INVALID KEY MOVE 23 TO PA-RETURN-CODE
   END-START
   PERFORM UNTIL LS-saleID = PA-LAST-SALEID
                 OR PA-LAST-SALEID = 0
      READ INDEX-FILE-SALE NEXT INTO LS-Sale
         AT END MOVE 10 TO PA-RETURN-CODE
                MOVE "NO" TO MORE-RECORDS
   END-PERFORM
   PERFORM TEST AFTER UNTIL (MORE-RECORDS = "NO"
                 OR LS-saleDate >= PA-LAST-DATE)
                 AND PA-RETURN-CODE NOT = 23
      READ INDEX-FILE-SALE NEXT INTO LS-Sale
         AT END MOVE 10 TO PA-RETURN-CODE
                MOVE "NO" TO MORE-RECORDS
         *> 11 is an unused status code, so I'm using it
         *> to signify there are more values
         NOT AT END MOVE 11 TO PA-RETURN-CODE
      END-READ
   END-PERFORM
   CLOSE INDEX-FILE-SALE
EXIT PARAGRAPH.

END PROGRAM READ_SALE.
